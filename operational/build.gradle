/*
 * Copyright (c) 2017 Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 */


//plugins {
  //  id 'com.github.johnrengelman.shadow' version '2.0.3'
//}

apply plugin: "distribution"
apply plugin: "java"
apply plugin: "maven"
apply plugin: "application"
apply plugin: "maven-publish"
apply plugin: 'com.github.johnrengelman.shadow'

group = "com.dellemc.oe.operations"
version = samplesVersion
archivesBaseName = "operational"
description = "operational"
sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
    compile project(':common')
    compile "io.pravega:pravega-client:${pravegaVersion}"
    if (includePravegaCredentials.toBoolean()) {
        compile "io.pravega:pravega-keycloak-credentials:${pravegaCredentialsVersion}"
    }
}
// Build examples

task scriptScopeCreator(type: CreateStartScripts) {
    outputDir = file('build/scripts')
    mainClassName = 'com.dellemc.oe.operations.ScopeCreator'
    applicationName = 'scopeCreator'
    classpath = files(jar.archivePath) + sourceSets.main.runtimeClasspath
}

task scriptStreamCreator(type: CreateStartScripts) {
    outputDir = file('build/scripts')
    mainClassName = 'com.dellemc.oe.operations.StreamCreator'
    applicationName = 'streamCreator'
    classpath = files(jar.archivePath) + sourceSets.main.runtimeClasspath
}

task startScopeCreator(type: JavaExec) {
    main = "com.dellemc.oe.operations.ScopeCreator"
    classpath = sourceSets.main.runtimeClasspath
    if(System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}

task startStreamCreator(type: JavaExec) {
    main = "com.dellemc.oe.operations.StreamCreator"
    classpath = sourceSets.main.runtimeClasspath
    if(System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}

shadowJar {
    dependencies {
        exclude(dependency("org.scala-lang:scala-library"))
        exclude(dependency("org.scala-lang:scala-reflect"))
        exclude(dependency("org.scala-lang:scala-compiler"))
    }
    classifier = ""
    zip64 true
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            into('bin') {

            }
            into('lib') {
                from(jar)
                from(project.configurations.runtime)
                from shadowJar
                from(project.configurations.shadow)
            }
            into('conf') {
                from(project.file('src/main/resources/logback.xml'))
            }
        }
    }
}